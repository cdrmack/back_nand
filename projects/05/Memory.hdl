// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/05/Memory.hdl

/**
 * The complete address space of the Hack computer's memory,
 * including RAM and memory-mapped I/O.
 * The chip facilitates read and write operations, as follows:
 *     Read:  out(t) = Memory[address(t)](t)
 *     Write: if load(t-1) then Memory[address(t-1)](t) = in(t-1)
 * In words: the chip always outputs the value stored at the memory
 * location specified by address. If load==1, the in value is loaded
 * into the memory location specified by address. This value becomes
 * available through the out output from the next time step onward.
 * Address space rules:
 * Only the upper 16K+8K+1 words of the Memory chip are used.
 * Access to address>0x6000 is invalid. Access to any address in
 * the range 0x4000-0x5FFF results in accessing the screen memory
 * map. Access to address 0x6000 results in accessing the keyboard
 * memory map. The behavior in these addresses is described in the
 * Screen and Keyboard chip specifications given in the book.
 */

CHIP Memory {
    IN in[16], load, address[15];
    OUT out[16];

    PARTS:
    // if NOT(addr[14]) then RAM
    // if addr[14] && NOT(addr[13]) then Screen
    // if addr[14] && addr[13] then Keyboard
    And(a=address[14], b=true, out=notRamAddress);
    Not(in=notRamAddress, out=ramAddress);
    And(a=ramAddress, b=load, out=loadRam);

    And(a=address[13], b=true, out=notScreenAddress);
    Not(in=notScreenAddress, out=screenAddressPartial);
    And(a=address[14], b=screenAddressPartial, out=screenAddressFinal);
    And(a=screenAddressFinal, b=load, out=loadScreen);

    And(a=address[14], b=address[13], out=loadKeyboard);

    RAM16K(in=in, load=loadRam, address=address[0..13], out=outRam);
    Screen(in=in, load=loadScreen, address=address[0..12], out=outScreen);
    Keyboard(out=outKeyboard);

    Mux4Way16(a=outKeyboard, b=outRam, c=outScreen, d=false,
             sel[0]=ramAddress, sel[1]=screenAddressFinal, out=out);
}
